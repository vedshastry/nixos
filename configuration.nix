{ config, pkgs, inputs, ... }:

{
  imports = [ ./hardware-configuration.nix ]; # Generated by the installer

  # Networking
  networking.hostName = "thinkpad";
  networking.networkmanager.enable = true; # nm-cli applet needs this

  # Time & Locale
  time.timeZone = "America/Los_Angeles";
  i18n.defaultLocale = "en_US.UTF-8";
  i18n.extraLocaleSettings = {
    LC_MONETARY = "en_US.UTF-8";
    LC_TELEPHONE = "en_US.UTF-8";
    LC_TIME = "en_US.UTF-8";
  };

  # User Account
  users.users.ved = {
    isNormalUser = true;
    uid = 1000;
    description = "Ved Shastry";
    extraGroups = [ "networkmanager" "wheel" "docker" "video" "audio" ];
    shell = pkgs.zsh;
  };

  # Graphics & Hardware
  hardware.graphics = {
    enable = true;
    enable32Bit = true;
  };
  services.fprintd.enable = true; # Fingerprint reader
  services.printing.enable = true; # Enable CUPS to print documents.

  # Enable sound with pipewire.
  services.pulseaudio.enable = false;
  security.rtkit.enable = true;
  services.pipewire = {
    enable = true;
    alsa.enable = true;
    alsa.support32Bit = true;
    pulse.enable = true;
    # If you want to use JACK applications, uncomment this
    #jack.enable = true;

    # use the example session manager (no others are packaged yet so this is enabled by default,
    # no need to redefine it in your config for now)
    #media-session.enable = true;
  };

  # Cloudflare WARP (Systemd Service)
  systemd.packages = [ pkgs.cloudflare-warp ];
  systemd.services.warp-svc.wantedBy = [ "multi-user.target" ];

  # Allow Unfree Software
  nixpkgs.config.allowUnfree = true;

# Allow Insecure Packages
  nixpkgs.config.permittedInsecurePackages = [
    "pulsar-1.129.0"  # Pulsar editor
  ];

  # System Packages (The "Base" layer)
  environment.systemPackages = with pkgs; [

	# Utilities
	brightnessctl

    # Suckless dependencies
    xorg.libX11 xorg.libXft xorg.libXinerama

    # Suckless Basics (Standard versions for now)
      st
      dmenu
      dwm
      slstatus

      # Workflow
      #pulsar       # Editor
      git
      wget
      vim
      neovim
      ranger
      ripgrep
      fd
      cryptsetup
      zsh

      # System Management
      pavucontrol  # Audio GUI
      htop

  ];

  services.xserver = {
    enable = true;
    windowManager.dwm.enable = true;
    displayManager.startx.enable = true; # Keep it simple, or use a DM like sddm
  };
  services.libinput.enable = true; # Enable touchpad support

  # enable zsh
  programs.zsh.enable=true;

  # suckless tools
  nixpkgs.overlays = [
    (final: prev: {

      dwm = prev.dwm.overrideAttrs (old: {
        src = inputs.my-dwm;
        nativeBuildInputs = (old.nativeBuildInputs or []) ++ [ prev.git prev.pkg-config ];
        buildInputs = (old.buildInputs or []) ++ [ prev.xorg.libxcb prev.xorg.xcbutil prev.xorg.xcbutilwm ];

        # Clean old binaries
        preBuild = ''
          make clean
        '';

        # --- THE FIX ---
        # Instead of relying on 'make install' and 'makeFlags', we manually copy the binary.
        # This guarantees it ends up in $out/bin/dwm where NixOS expects it.
        installPhase = ''
          mkdir -p $out/bin
          cp dwm $out/bin/

          # Optional: Copy man pages if you want 'man dwm' to work
          mkdir -p $out/share/man/man1
          cp dwm.1 $out/share/man/man1/
        '';
      });

      st = prev.st.overrideAttrs (old: {
        src = inputs.my-st;
        nativeBuildInputs = (old.nativeBuildInputs or []) ++ [ prev.git prev.pkg-config ];
	      buildInputs = (old.buildInputs or []) ++ [ prev.harfbuzz ];

        # Clean old binaries
        preBuild = ''
          make clean
        '';

        postPatch = ''
        ${old.postPatch or ""}
        sed -i '/git submodule/d' Makefile
        '';
      });

      dmenu = prev.dmenu.overrideAttrs (old: {
        src = inputs.my-dmenu;
        nativeBuildInputs = (old.nativeBuildInputs or []) ++ [ prev.git ];

        # Clean old binaries
        preBuild = ''
          make clean
        '';

      });

      slstatus = prev.slstatus.overrideAttrs (old: {
        src = inputs.my-slstatus;
        nativeBuildInputs = (old.nativeBuildInputs or []) ++ [ prev.git ];

        # Clean old binaries
        preBuild = ''
          make clean
        '';

      });
    })
  ];

  # Fonts
  fonts = {
	  packages = with pkgs; [
	    noto-fonts
	    noto-fonts-cjk-sans
	    noto-fonts-color-emoji
	    font-awesome
	    nerd-fonts.jetbrains-mono
	    nerd-fonts.symbols-only
	  ];

	  fontconfig = {
	    enable = true;
	    defaultFonts = {
	      monospace = [ "JetBrainsMono Nerd Font" ];
	      serif = [ "Noto Serif" ];
	      sansSerif = [ "Noto Sans" ];
	    };
	  };
  };

  # Essential Hardware Tweaks (ThinkPad T14 Gen 5)
  boot.kernelPackages = pkgs.linuxPackages_latest; # 6.12+ kernel for HawkPoint APU
  services.fwupd.enable = true; # Firmware updates

  # Leave at the release version of first install (25.11)
  # Before changing this value read the documentation for this option
  # (e.g. man configuration.nix or on https://nixos.org/nixos/options.html).
  system.stateVersion = "25.11";
}
